---

description: CircleCI プロジェクト設定の設定ポリシーの管理
contentTags:
  platform:
  - クラウド
---
= 設定ポリシーの管理機能の概要
:page-layout: classic-docs
:page-liquid:
:icons: font
:toc: macro

:toc-title:

NOTE: 設定ポリシーの管理機能が **Scale** プランでご利用いただけるようになりました。この機能は現在 **オープンプレビュー** であり、 すべての側面において、今後変更される可能性があります。

[#introduction]
== はじめに

設定ポリシーの管理機能により、CircleCI プロジェクトの設定を管理するポリシーを作成できます。 プロジェクトの設定が関連付けられたポリシーに定められたルールに準拠していない場合、最も厳しいケースでは、準拠するまでそのプロジェクトのパイプラインはトリガーできません。

CircleCI では、`config.yml` ファイルを使ってCI/CD パイプラインをプロジェクトレベルで定義します。 この方法ではプロジェクトの拡大に伴ったニーズに合うように各パイプラインを作成できるため、開発や反復を迅速に行う際に便利です。 しかし、組織全体の規則やセキュリティポリシーを管理および適用することが困難な場合があります。 そんな場合に、設定ポリシーの管理機能が役立ちます。

設定ポリシーに関する判断は保存され、監査できます。 これにより、組織内で実行されているパイプラインの定義に関する有益なデータが得られます。

[#how-config-policy-management-works]
== 設定ポリシー管理の仕組み

設定ポリシーの管理機能では、link:https://www.openpolicyagent.org/[Open Policy Agent (OPA)] に基づき意思決定エンジンを使用します。 ポリシーは、OPA の定義に従い Rego クエリ言語で記述されます。 ルール違反があった場合は、パイプラインがトリガーされる際に表面化します。

ポリシーはローカルで作成し、CircleCI CLI を使って CircleCI にプッシュすることができます。 ポリシーは ご自身の VCS 内のリポジトリに保存できます。 詳細については、 link:/docs/use-the-cli-and-vcs-for-config-policy-management[設定ポリシーの管理における CLI と VCS の使用] のページを参照してください。 

[#writing-rego-policies-using-circleci-domain-specific-language]
== CircleCI ドメイン固有の言語を使った Rego ポリシーの記述

ポリシーは、OPA をサポートする専用の宣言型ポリシー言語である Rego で記述されます。 Rego の詳細については、link:https://www.openpolicyagent.org/docs/latest/policy-language/[`rego` 言語に関するドキュメント] を参照してください。

CircleCI が設定を判断するためには、ポリシーの評価結果を CircleCI が解釈できる必要があります。 そのため、このページに概説されている CircleCI の仕様に合うようにポリシーを記述する必要があります。

[#package-and-name]
=== パッケージと名前

すべてのポリシーが  `org` パッケージに属し、ポリシー名を最初のルールとして宣言する必要があります。 ポリシーのすべての Rego ファイルには以下が含まれている必要があります。

[source,rego]
----
package org

policy_name["unique_policy_name"]
----

`policy_name` は最大 80 文字の英数字の文字列です。 CircleCI においてポリシーを名前で一意に識別するために使用されます (Kubernetes のリソース名と同様)。
ポリシー名は一意である必要があります。 1つの組織内で 2 つのポリシーに同じ名前をつけることはできません。

`policy_name` は一部のルールを使用して宣言し、rego key`: `policy_name["NAME"]` として宣言する必要があります。

[#rules]
=== ルール

`org` パッケージと `policy_name` ルールを宣言したら、ポリシーをルールの一覧として定義できます。 各ルールは 3 つのパートで構成されます。

* 評価: 設定にポリシー違反がないかを評価
* 評価結果: 違反した場合の対処方法
* 有効化: 評価結果を有効にするかどうかを決定

このフォーマットを使用すると、ポリシー作成者 は、CircleCI のポリシー評価の出力を解析する能力に影響を与えることなく、カスタムヘルパー関数を作成できます。 ポリシーはすべて `input` 変数を使って設定データにアクセスできます。  この `input` が評価されるプロジェクト設定です。  'input' は CircleCI の設定と一致するので、'jobs' や 'workflows' などの使用可能な任意の設定要素にご希望のステータスを適用するルールを記述することができます。

[source,rego]
----
input.workflows     # an array of nested structures mirroring workflows in the CircleCI config
input.jobs          # an array of nested structures mirroring jobs in the CircleCI config
----

[#define-a-rule]
==== ルールの定義

OPA では、ルールよってあらゆるタイプの出力を生成できます。 CircleCI では、違反の対象となるルールには、以下のタイプの出力が必要です。

* 文字列
* 文字列配列
* 文字列から文字列のマップ

これは、ルール違反があった場合に、個々の投稿者や SecOps がそれに基づき対応するためのエラーメッセージが生成される必要があるためです。
別のタイプの出力を生成するヘルパールールを定義することもできますが、CircleCI が判断時に考慮するルールには、上記の出力タイプが必要です。 詳細については、下記の <<#enablement>> のセクションをご覧ください。

[#evaluation]
===== 評価

設定が規定されたポリシーに違反した場合に、意思決定エンジンがどのように判断するかを説明します。 評価では、ルールの名前と ID を定義し、条件をチェックして、違反について記載するユーザーフレンドリーな文字列を返します。 ルールの評価には、**ルール名** と **オプションのルール ID** が含まれます。 ルール名は、ルールの評価結果を有効化し、設定するために使用されます。

[source,rego]
----
RULE_NAME = reason {
  ... # some comparison
  reason := "..."
}
----

[source,rego]
----
RULE_NAME[RULE_ID] = reason {
  ... # some comparison
  reason := "..."
}
----

下記は、設定に少なくとも 1 つのワークフローが含まれる設定をチェックするシンプルな評価の例です。

[source,rego]
----
contains_workflows = reason {
    count(input.workflows) > 0
    reason := "config must contain at least one workflow"
}
----

ルール ID は同一のルールについて複数の違反があった場合に識別するために使用されます。 たとえば、設定で非公式の Docker イメージを使用している場合、`use_official_docker_image` ルールに複数回違反している状態になる場合があります。 ルール ID は複数の違反が予測される場合のみ使用してください。 場合によっては、ルールに適用したかどうかだけを知りたい場合もあります。 その場合、ルール ID は不要です。

[source,rego]
----
use_official_docker_image[image] = reason {
  some image in docker_images   # docker_images are parsed below
  not startswith(image, "circleci")
  not startswith(image, "cimg")
  reason := sprintf("%s is not an approved Docker image", [image])
}

# helper to parse docker images from the config
docker_images := {image | walk(input, [path, value])  # walk the entire config tree
                          path[_] == "docker"         # find any settings that match 'docker'
                          image := value[_].image}    # grab the images from that section

----

[#enforcement]
===== 評価結果

このポリシーの管理機能では、ルールを様々なレベルで設定できます。

[source,rego]
----
ENFORCEMENT_STATUS["RULE_NAME"]
----

設定可能なレベルは 以下の 2 つのレベルです。

* `hard_fail` : `policy-service` が設定において `hard_fail` と設定されたルールの違反を検出した場合、パイプラインはトリガーされません。
* `soft_fail` : `policy-service` が設定において `soft_fail` と設定されたルールの違反を検出した場合、パイプラインはトリガーされますが、ルール違反は `policy-service` の判断ログに記録されます。

 `hard_fail` に設定された `use_official_docker_image` ルールの設定例:

[source,rego]
----
hard_fail["use_official_docker_image"]
----

[#enablement]
===== 有効化

ポリシー違反を審査するためにはルールを有効化する必要があります。 有効化されていないルールは、CircleCI 違反出力形式に合致する必要はなく、他のルールのヘルパーとして使用できます。

[source,rego]
----
enable_rule["RULE_NAME"]
----

ルールを有効化するには、そのルールを `enable_rule` オブジェクトにキーとして追加します。 たとえば、`use_official_docker_image` ルールを有効化する場合、以下を使用します。

[source,rego]
----
enable_rule["use_official_docker_image"]
----

[#using-pipeline-metadata]
=== パイプラインのメタデータの使用

CircleCI 設定ファイルのポリシーを記述する場合、多くの場合プロジェクトやブランチごとに動作が若干変わるポリシーにすることが推奨されます。 それには `data.meta` Rego プロパティーを使用します。

ポリシーがトリガーされたパイプラインのコンテキストで評価されると、`data.meta` において以下の 3 つのプロパティーが使用できるようになります。

[source,shell]
----
project_id    (CircleCI Project UUID)
branch        (string)
build_number  (number)
----

このメタデータは、ルールのアクティブ化/非アクティブ化、評価結果の変更、およびルール定義自体の一部として使用できます。

以下は 1 つのプロジェクトに対してのみルールを実行し、ブランチメイン上でのみ `hard_fail` とするポリシーのサンプルです。

[source,rego]
----
package org

policy_name["example"]

# specific project UUID
# use care to avoid naming collisions as assignments are global across the entire policy bundle
sample_project_id := "c2af7012-076a-11ed-84e6-f7fa45ad0fd1"

# this rule is enabled only if the body is evaluates to true
enable_rule["custom_rule"] { data.meta.project_id == sample_project_id }

# "custom_rule" evaluates to a hard_failure condition only if run in the context of branch main
hard_fail["custom_rule"] { data.meta.branch == "main" }
----

[#example-policy]
== サンプルポリシー

以下は、`use_official_docker_image` という 1 つのルールを含む全ポリシーのサンプルです。設定ファイル内の先頭に `circleci` や `cimg` がついたすべての Docker イメージをチェックします。 設定ファイル内のすべての `docker_images` を見つけるヘルパーコードを使用しています。  `use_official_docker_image` の評価結果を `hard_fail` に設定し、このルールを有効化しています。

[source,rego]
----
package org

import future.keywords

policy_name["example"]

use_official_docker_image[image] = reason {
  some image in docker_images   # docker_images are parsed below
  not startswith(image, "circleci")
  not startswith(image, "cimg")
  reason := sprintf("%s is not an approved Docker image", [image])
}

# helper to parse docker images from the config
docker_images := {image | walk(input, [path, value])  # walk the entire config tree
                          path[_] == "docker"         # find any settings that match 'docker'
                          image := value[_].image}    # grab the images from that section

hard_fail["use_official_docker_image"]

enable_rule["use_official_docker_image"]
----

[#next-steps]
== 次のステップ

*  link:/docs/use-the-cli-and-vcs-for-config-policy-management[設定ポリシーの管理における CLI と VCS の使用] 
* link:/docs/use-the-cli-for-config-and-policy-development[設定とポリシーの作成における CircleCI CLI の使用]
* link:/docs/config-policy-reference[設定ポリシーのリファレンス]